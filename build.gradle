buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'	
    }
}

plugins {
	id "java"
	id "idea"
	id "eclipse"
	id "application"
	id "jacoco"
	id "com.github.kt3k.coveralls" version "2.4.0"
	id "maven"
	id "maven-publish"
	id "com.jfrog.bintray" version "1.3.1"
}


publishing {
	publications {
		Bintray(MavenPublication) {
			from components.java

            artifact(sourceJar) {
                classifier "sources"
            }

            artifact(javadocJar) {
                classifier "javadoc"
            }

            groupId 'de.uniba.dsg'
            artifactId 'bpmnspector'
            version '1.1.0'
		}
	}
}

bintray {
	user = 'bintray_user'
    key = 'bintray_api_key'
	publications = ['Bintray']
    pkg {
        repo = 'BPMNspector'
        name = 'BPMNspector'
        userOrg = 'uniba-dsg'
        licenses = ['LGPL-3.0']
        vcsUrl = 'https://github.com/uniba-dsg/BPMNspector.git'
		version {
            name = 'v1.1.0'
            desc = 'BPMNspector v1.1.0 - Static analysis for BPMN 2.0 process models.'
            vcsTag = 'v1.1.0'
        }
    }
}

configurations {
    compile
    external
}

repositories {
    mavenCentral()
    maven {
        url "https://artefacts.alfresco.com/nexus/content/repositories/activiti-releases/"
    }
    maven { url 'https://jitpack.io' }
}

dependencies {
	compile 'org.jdom:jdom2:2.0.6'
	compile 'jaxen:jaxen:1.1.6'
	compile 'ch.qos.logback:logback-core:1.2.+'
	compile 'ch.qos.logback:logback-classic:1.2.+'
	compile 'org.slf4j:slf4j-api:1.7.25'
	compile 'com.phloc:phloc-commons:4.4.11'
	compile 'commons-cli:commons-cli:1.4'
	compile 'commons-io:commons-io:2.5'
	compile 'org.apache.velocity:velocity:1.7'
    compile 'org.apache.logging.log4j:log4j-api:2.7'
    compile 'org.apache.logging.log4j:log4j-core:2.7'
    //compile 'com.github.guybrushPrince:mojo.core:master-SNAPSHOT'
    compile 'com.github.matthiasgeiger:mojo.core:master-SNAPSHOT'
    compile 'com.github.guybrushPrince:mojo.reader.bpmn:master-SNAPSHOT'

    external files(fileTree(dir: "lib", includes: ['*.jar']))

	testCompile 'junit:junit:4.+'

	compile configurations.external
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

mainClassName = "de.uniba.dsg.bpmnspector.BPMNspectorMain"

version = "1.1.0"

run {
    if(project.hasProperty('args')){
        args project.args.split('\\s+')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0.2'
}


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}


jar.archiveName = "BPMNspector.jar"

jar {
	manifest { attributes 'Main-Class': mainClassName }
	from { configurations.external.collect { it.isDirectory() ? it : zipTree(it) } }
}

task sourceJar(type: Jar) {
    baseName = project.name + '-sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    baseName = project.name + '-javadoc'
    from javadoc.destinationDir
}

task fatJar(type: Jar) {
    manifest.from jar.manifest
    baseName = project.name + '-fat'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}


jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}